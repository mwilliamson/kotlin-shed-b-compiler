// stdout:
//   1
//   2
//   3

union List[T] =
    | Cons[T] {
        head: T,
        tail: List[T],
    }
    | Nil;

val nil = Nil();

fun cons[T](head: T, tail: List[T]) -> List[T] {
    Cons(head=head, tail=tail)
}

fun printInts(list: List[Int]) !Io -> Unit {
    val strings = map(intToString, list);
    printStrings(strings);
}

fun printStrings(list: List[String]) !Io -> Unit {
    if (list is Cons[String]) {
        print(list.head + "\n");
        printStrings(list.tail);
    }
}

fun map[T, R, !E](func: (T) !E -> R, list: List[T]) !E -> List[R] {
    if (list is Cons[T]) {
        cons(func(list.head), map(func, list.tail))
    } else {
        nil
    }
}

fun main() !Io -> Unit {
    val list = cons(1, cons(2, cons(3, nil)));
    printInts(list);
}
