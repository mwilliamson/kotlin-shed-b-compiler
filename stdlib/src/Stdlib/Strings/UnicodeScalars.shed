export
    unicodeScalarToHexString,
    unicodeScalarToInt,
    unicodeScalarToString,
    count,
    first,
    flatMap,
    foldLeft,
    replace,
    substring,

    eq,
    dropLeft,
    isEmpty,
    join,
    left,
    leftWhile,
    padLeft,
    startsWith;

import Lists from Stdlib.Lists;
import @(.List as List) from Stdlib.Lists;
import Options from Stdlib.Options;
import Strings from Stdlib.Platform.Strings;
import Predicates from Stdlib.Predicates;
import Repeat from Stdlib.Repeat;
import Sequences from Stdlib.Sequences;

val unicodeScalarToHexString = Strings.unicodeScalarToHexString;
val unicodeScalarToInt = Strings.unicodeScalarToInt;
val unicodeScalarToString = Strings.unicodeScalarToString;
val count = Strings.unicodeScalarCount;
val replace = Strings.replace;
val substring = Strings.substring;

fun eq(left: String, right: String) -> Bool {
    left == right
}

fun stringToSequence(value: String) -> Sequences.Sequence[UnicodeScalar, Pure] {
    stringSliceToSequence(Strings.slice(value))
}

fun stringSliceToSequence(value: StringSlice) -> Sequences.Sequence[UnicodeScalar, Pure] {
    Sequences.pure[UnicodeScalar](fun () {
        val next = Strings.next(value);
        when (next) {
            is None {
                Sequences.end
            }
            is Some {
                val #(unicodeScalar, rest) = next.value;
                Sequences.item(unicodeScalar, stringSliceToSequence(rest))
            }
        }
    })
}

fun dropLeft(toDrop: Int, value: String) -> String {
    Strings.dropLeftUnicodeScalars(
        toDrop,
        value,
    )
}

fun first(value: String) -> Option[UnicodeScalar] {
    val next = Strings.next(Strings.slice(value));
    when (next) {
        is Some {
            val #(unicodeScalar, rest) = next.value;
            some(unicodeScalar)
        }
        is None {
            none
        }
    }
}

fun flatMap(func: Fun (UnicodeScalar) -> String, value: String) -> String {
    foldLeft(
        fun (acc: String, unicodeScalar: UnicodeScalar) => acc + func(unicodeScalar),
        "",
        value,
    )
}

fun foldLeft[T](func: Fun (T, UnicodeScalar) -> T, initial: T, value: String) -> T {
    Sequences.foldLeft(
        func,
        initial,
        stringToSequence(value),
    )
}

fun isEmpty(value: String) -> Bool {
    value == ""
}

fun join(separator: String, elements: List[String]) -> String {
    Lists.foldLeft(
        fun (text: String, element: String) -> String {
            if (text == "") {
                element
            } else {
                text + separator + element
            }
        },
        "",
        elements
    )
}

fun left(count: Int, value: String) -> String {
    Strings.substring(0, count, value)
}

fun leftWhile(predicate: Fun (UnicodeScalar) -> Bool, value: String) -> String {
    val index = Sequences.findIndex(Predicates.negate(predicate), stringToSequence(value));
    when (index) {
        is None {
            value
        }
        is Some {
            left(index.value, value)
        }
    }
}

fun padLeft(desiredLength: Int, padding: UnicodeScalar, string: String) -> String {
    repeat(unicodeScalarToString(padding), desiredLength - count(string)) + string
}

fun repeat(value: String, times: Int) -> String {
    Repeat.repeat(fun (acc: String) => acc + value, times, "")
}

fun startsWith(prefix: String, value: String) -> Bool {
    left(count(prefix), value) == prefix
}
