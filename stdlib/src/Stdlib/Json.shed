export
    JsonValue,
    JsonObject,
    JsonArray,
    JsonNumber,
    JsonString,
    JsonBoolean,
    JsonNull,
    JsonProperty,

    object,
    array,
    number,
    string,
    boolean,
    null,
    property,

    valueToText,
    JsonResult,
    JsonSuccess,
    JsonFailure,
    textToValue;

import ListBuilder from .ListBuilder;
import Lists from .Lists;
import @(.list as list, .List as List) from .Lists;
import StringBuilder from .Platform.StringBuilder;
import Sequences from .Sequences;
import Unfold from .Sequences.Unfold;
import UnicodeScalars from .Strings.UnicodeScalars;

union JsonValue =
    | JsonObject {
        properties: List[JsonProperty],
    }
    | JsonArray {
        elements: List[JsonValue],
    }
    | JsonNumber {
        value: String,
    }
    | JsonString {
        value: String,
    }
    | JsonBoolean {
        value: Bool,
    }
    | JsonNull;

fun object(properties: List[JsonProperty]) -> JsonValue {
    JsonObject(.properties = properties)
}

shape JsonProperty {
    key: String,
    value: JsonValue,
}

fun property(key: String, value: JsonValue) -> JsonProperty {
    JsonProperty(.key = key, .value = value)
}

fun array(elements: List[JsonValue]) -> JsonValue {
    JsonArray(.elements = elements)
}

fun number(value: String) -> JsonValue {
    JsonNumber(.value = value)
}

fun string(value: String) -> JsonString {
    JsonString(.value = value)
}

fun boolean(value: Bool) -> JsonValue {
    JsonBoolean(.value = value)
}

val null = JsonNull();

fun valueToText(value: JsonValue) -> String {
    StringBuilder.build(fun () !StringBuilder.Write -> Unit {
        writeJsonValue!(value)
    })
}

fun writeJsonValue(value: JsonValue) !StringBuilder.Write -> Unit {
    when (value) {
        is JsonObject {
            StringBuilder.write!("{");
            Lists.forEachInterspersed!(
                writeJsonProperty,
                fun () !StringBuilder.Write -> Unit {
                    StringBuilder.write!(",");
                },
                value.properties,
            );
            StringBuilder.write!("}");
        }
        is JsonArray {
            StringBuilder.write!("[");
            Lists.forEachInterspersed!(
                writeJsonValue,
                fun () !StringBuilder.Write -> Unit {
                    StringBuilder.write!(",");
                },
                value.elements,
            );
            StringBuilder.write!("]");
        }
        is JsonNumber {
            StringBuilder.write!(value.value);
        }
        is JsonString {
            writeJsonString!(value.value);
        }
        is JsonBoolean {
            StringBuilder.write!(if (value.value) {
                "true"
            } else {
                "false"
            });
        }
        is JsonNull {
            StringBuilder.write!("null");
        }
    }
}

fun writeJsonProperty(property: JsonProperty) !StringBuilder.Write -> Unit {
    writeJsonString!(property.key);
    StringBuilder.write!(":");
    writeJsonValue!(property.value);
}

fun writeJsonString(value: String) !StringBuilder.Write -> Unit {
    StringBuilder.write!(jsonStringToText(value));
}

fun jsonStringToText(value: String) -> String {
    val characters = UnicodeScalars.flatMap(jsonStringUnicodeScalarToText, value);
    "\"" + characters + "\""
}

fun jsonStringUnicodeScalarToText(unicodeScalar: UnicodeScalar) -> String {
    if (unicodeScalar == '\\') {
        "\\\\"
    } else if (unicodeScalar == '\"') {
        "\\\""
    } else if (unicodeScalar == '\t') {
        "\\t"
    } else if (unicodeScalar == '\n') {
        "\\n"
    } else if (unicodeScalar == '\r') {
        "\\r"
    } else if (unicodeScalar < '\u{20}') {
        val hexString = UnicodeScalars.unicodeScalarToHexString(unicodeScalar);
        "\\u" + UnicodeScalars.padLeft(4, '0', hexString)
    } else {
        UnicodeScalars.unicodeScalarToString(unicodeScalar)
    }
}

fun stringTextToUnicodeScalars(text: String) -> String {
    // TODO: handle escape sequences
    UnicodeScalars.substring(1, -1, text)
}

union JsonResult =
    | JsonSuccess {
        value: JsonValue,
    }
    | JsonFailure;

fun textToValue(text: String) -> JsonResult {
    val tokens = Sequences.lazyFilter(
        fun (token: Token) => not (token.tokenType is TokenWhitespace),
        tokenise(text),
    );
    tokensToValue(createTokenIterator(tokens))
}

fun tokensToValue(tokens: TokenIterator) -> JsonResult {
    val result = readValue(tokens);
    when (result) {
        is ParseSuccess {
            if (isNextTokenType(
                fun (tokenType: TokenType) => tokenType is TokenEnd,
                result.remainder,
            )) {
                JsonSuccess(.value = result.value)
            } else {
                JsonFailure()
            }
        }
        is ParseFailure {
            JsonFailure()
        }
    }
}

shape TokenIterator {
    head: Token,
    tail: Fun () -> TokenIterator,
}

union ParseResult[+T] =
    | ParseSuccess[T] {
        remainder: TokenIterator,
        value: T,
    }
    | ParseFailure {
        remainder: TokenIterator,
    };

fun success[T](remainder: TokenIterator, value: T) -> ParseResult[T] {
    ParseSuccess(.remainder = remainder, .value = value)
}

fun failure(tokens: TokenIterator) -> ParseResult[Bottom] {
    ParseFailure(.remainder = tokens)
}

fun readValue(tokens: TokenIterator) -> ParseResult[JsonValue] {
    when (tokens.head.tokenType) {
        is TokenTrue {
            success(tokens.tail(), boolean(true))
        }
        is TokenFalse {
            success(tokens.tail(), boolean(false))
        }
        is TokenNull {
            success(tokens.tail(), null)
        }
        is TokenString {
            parseStringBody(tokens)
        }
        is TokenOpenSquare {
            val elementsResult = parseMany(
                .parseElement = readValue,
                .parseSeparator = skipTokenType ~ (fun (tokenType: TokenType) => tokenType is TokenComma),
                .isEnd = isNextTokenType ~ (fun (tokenType: TokenType) => tokenType is TokenCloseSquare),
            )(tokens.tail());

            when (elementsResult) {
                is ParseSuccess {
                    val closeResult = skipTokenType(
                        fun (tokenType: TokenType) => tokenType is TokenCloseSquare,
                        elementsResult.remainder,
                    );
                    when (closeResult) {
                        is ParseSuccess {
                            success(closeResult.remainder, array(elementsResult.value))
                        }
                        is ParseFailure {
                            failure(closeResult.remainder)
                        }
                    }
                }

                is ParseFailure {
                    elementsResult
                }
            }
        }
        is TokenOpenBrace {
            parseObjectBody(tokens)
        }
        else {
            failure(tokens)
        }
    }
}

fun parseObjectBody(tokens: TokenIterator) -> ParseResult[JsonValue] {
    val propertiesResult = parseMany(
        .parseElement = parseProperty,
        .parseSeparator = skipTokenType ~ (fun (tokenType: TokenType) => tokenType is TokenComma),
        .isEnd = isNextTokenType ~ (fun (tokenType: TokenType) => tokenType is TokenCloseBrace),
    )(tokens.tail());

    when (propertiesResult) {
        is ParseSuccess {
            val closeResult = skipTokenType(
                fun (tokenType: TokenType) => tokenType is TokenCloseBrace,
                propertiesResult.remainder,
            );
            when (closeResult) {
                is ParseSuccess {
                    success(closeResult.remainder, object(propertiesResult.value))
                }
                is ParseFailure {
                    failure(closeResult.remainder)
                }
            }
        }

        is ParseFailure {
            propertiesResult
        }
    }
}

fun parseProperty(tokens: TokenIterator) -> ParseResult[JsonProperty] {
    val nameResult = parseString(tokens);
    when (nameResult) {
        is ParseSuccess {
            val name = nameResult.value.value;
            if (isNextTokenType(fun (tokenType: TokenType) => tokenType is TokenColon, nameResult.remainder)) {
                val valueResult = readValue(nameResult.remainder.tail());
                when (valueResult) {
                    is ParseSuccess {
                        success(valueResult.remainder, property(name, valueResult.value))
                    }
                    is ParseFailure {
                        valueResult
                    }
                }
            } else {
                failure(nameResult.remainder)
            }
        }
        is ParseFailure {
            nameResult
        }
    }
}

fun parseString(tokens: TokenIterator) -> ParseResult[JsonString] {
    if (tokens.head.tokenType is TokenString) {
        parseStringBody(tokens)
    } else {
        failure(tokens)
    }
}

fun parseStringBody(tokens: TokenIterator) -> ParseResult[JsonString] {
    success(tokens.tail(), string(stringTextToUnicodeScalars(tokens.head.value)))
}

union Position = Start | ElementStart | ElementEnd;

shape State[T] {
    elements: ListBuilder.Builder[T],
    position: Position,
    tokens: TokenIterator,
}

fun parseMany[T](
    .parseElement: Fun (TokenIterator) -> ParseResult[T],
    .parseSeparator: Fun (TokenIterator) -> ParseResult[Unit],
    .isEnd: Fun (TokenIterator) -> Bool,
) -> Fun (TokenIterator) -> ParseResult[List[T]] {
    fun parse(state: State[T]) -> ParseResult[List[T]] {
        when (state.position) {
            is Start {
                if (isEnd(state.tokens)) {
                    success(state.tokens, list())
                } else {
                    // TODO: use update()
                    parse(State(
                        .elements = state.elements,
                        .position = ElementStart(),
                        .tokens = state.tokens
                    ))
                }
            }
            is ElementStart {
                val elementResult = parseElement(state.tokens);
                when (elementResult) {
                    is ParseSuccess {
                        parse(State(
                            .elements = ListBuilder.append(state.elements, elementResult.value),
                            .position = ElementEnd(),
                            .tokens = elementResult.remainder,
                        ))
                    }
                    is ParseFailure {
                        elementResult
                    }
                }
            }
            is ElementEnd {
                if (isEnd(state.tokens)) {
                    success(state.tokens, ListBuilder.toList(state.elements))
                } else {
                    val separatorResult = parseSeparator(state.tokens);
                    when (separatorResult) {
                        is ParseSuccess {
                            parse(State(
                                .elements = state.elements,
                                .position = ElementStart(),
                                .tokens = separatorResult.remainder,
                            ))
                        }
                        is ParseFailure {
                            separatorResult
                        }
                    }
                }
            }
        }
    }

    fun (tokens: TokenIterator) -> ParseResult[List[T]] {
        val initialState = State[T](.elements = ListBuilder.builder(), .position = Start(), .tokens = tokens);
        parse(initialState)
    }
}

fun isNextTokenType(predicate: Fun (TokenType) -> Bool, tokens: TokenIterator) -> Bool {
    predicate(tokens.head.tokenType)
}

fun skipTokenType(predicate: Fun (TokenType) -> Bool, tokens: TokenIterator) -> ParseResult[Unit] {
    if (predicate(tokens.head.tokenType)) {
        success(tokens.tail(), unit)
    } else {
        failure(tokens)
    }
}

fun createTokenIterator(tokens: Sequences.Sequence[Token, Pure]) -> TokenIterator {
    val iterator = tokens.next();
    when (iterator) {
        is Sequences.SequenceEnd {
            TokenIterator(
                .head = token(TokenEnd(), ""),
                .tail = fun () => createTokenIterator(tokens),
            )
        }
        is Sequences.SequenceItem {
            TokenIterator(
                .head = iterator.head,
                .tail = fun () => createTokenIterator(iterator.tail),
            )
        }
    }
}

union TokenType =
    | TokenWhitespace
    | TokenNull
    | TokenTrue
    | TokenFalse
    | TokenString
    | TokenOpenSquare
    | TokenCloseSquare
    | TokenOpenBrace
    | TokenCloseBrace
    | TokenColon
    | TokenComma
    | TokenUnexpected
    | TokenEnd;

shape Token {
    tokenType: TokenType,
    value: String,
}

fun token(tokenType: TokenType, value: String) -> Token {
    Token(.tokenType = tokenType, .value = value)
}

fun nextToken(value: String) -> Token {
    val maybeFirst = UnicodeScalars.first(value);
    when (maybeFirst) {
        is Some {
            val first = maybeFirst.value;
            if (first == 't' && UnicodeScalars.startsWith("true", value)) {
                token(TokenTrue(), "true")
            } else if (first == 'f' && UnicodeScalars.startsWith("false", value)) {
                token(TokenFalse(), "false")
            } else if (first == 'n' && UnicodeScalars.startsWith("null", value)) {
                token(TokenNull(), "null")
            } else if (first == '"') {
                val string = UnicodeScalars.leftWhile(fun (unicodeScalar: UnicodeScalar) => unicodeScalar != '"', UnicodeScalars.dropLeft(1, value));
                token(TokenString(), "\"" + string + "\"")
            } else if (first == '[') {
                token(TokenOpenSquare(), "[")
            } else if (first == ']') {
                token(TokenCloseSquare(), "]")
            } else if (first == '{') {
                token(TokenOpenBrace(), "}")
            } else if (first == '}') {
                token(TokenCloseBrace(), "}")
            } else if (first == ',') {
                token(TokenComma(), ",")
            } else if (first == ':') {
                token(TokenColon(), ":")
            } else if (isWhitespace(first)) {
                val whitespace = value |> UnicodeScalars.leftWhile ~ (isWhitespace);
                token(TokenWhitespace(), whitespace)
            } else {
                token(TokenUnexpected(), UnicodeScalars.left(1, value))
            }
        }
        is None {
            token(TokenEnd(), "")
        }
    }
}

fun isWhitespace(value: UnicodeScalar) -> Bool {
    value == ' ' || value == '\n' || value == '\r' || value == '\t'
}

val tokenise = Unfold.unfold ~ (fun (value: String) -> Unfold.Next[Token, String] {
    val token = nextToken(value);
    if (token.tokenType is TokenEnd) {
        Unfold.end
    } else {
        val remainder = UnicodeScalars.dropLeft(UnicodeScalars.count(token.value), value);
        Unfold.continue(token, remainder)
    }
});
