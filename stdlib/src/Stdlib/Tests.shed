export assertThat, Test, TestCase, TestSuite, suite, testCase, runTests;

import .Lists;
import .Matchers;
import .Options;


shape AssertionError {
    message: String,
}

fun fail(message: String) -> AssertionResult {
    AssertionResult(error = Options.some(AssertionError(message = message)))
}

fun assert(value: Bool) -> AssertionResult {
    if (value) {
        AssertionResult(error = Options.none)
    } else {
        fail("value was false")
    }
}

fun assertThat[T](value: T, matcher: Matchers.Matcher[T]) -> AssertionResult {
    val result = matcher.match(value);
    when (result) {
        is Matchers.Failure {
            val message = "expected " + matcher.description + "\nbut " + result.message;
            val error = AssertionError(message = message);
            AssertionResult(error = Options.some(error))
        }
        is Matchers.Success {
            AssertionResult(error = Options.none)
        }
    }
}

shape AssertionResult {
    error: Options.Option[AssertionError],
}

union Test =
    | TestCase {
        name: String,
        run: Fun () -> AssertionResult,
    }
    | TestSuite {
        name: String,
        tests: List[Test],
    };

union TestResult =
    | TestCaseResult {
        name: String,
        error: Options.Option[AssertionError],
    }
    | TestSuiteResult {
        name: String,
        results: List[TestResult],
    };

fun testCase(name: String, run: Fun () -> AssertionResult) -> TestCase {
    TestCase(name = name, run = run)
}

fun suite(name: String, tests: List[Test]) -> Test {
    TestSuite(name = name, tests = tests)
}

val greenCode = "\u{1B}[32m";
val redCode = "\u{1B}[31m";
val blackCode = "\u{1B}[39m";

fun runTests(tests: List[Test]) !Io -> Int {
    val results = Lists.map!(runTest ~ (parent = Options.none), tests);
    print!("\n");
    Lists.forEach!(printFailures, results);

    val counts = countTestResults(results);

    print!(intToString(counts.successes) + " tests passed, ");
    print!(intToString(counts.failures) + " tests failed\n");

    if (counts.failures == 0) {
        0
    } else {
        1
    }
}

fun countTestResults(results: List[TestResult]) -> TestCounts {
    Lists.reduce(
        fun (counts: TestCounts, result: TestResult) =>
            addTestCounts(counts, countTestResult(result)),
        TestCounts(failures = 0, successes = 0),
        results,
    )
}

fun countTestResult(result: TestResult) -> TestCounts {
    when (result) {
        is TestCaseResult {
            when (result.error) {
                is Options.Some[AssertionError] {
                    TestCounts(failures = 1, successes = 0)
                }
                is Options.None {
                    TestCounts(failures = 0, successes = 1)
                }
            }
        }
        is TestSuiteResult {
            countTestResults(result.results)
        }
    }
}

fun addTestCounts(counts1: TestCounts, counts2: TestCounts) -> TestCounts {
    TestCounts(
        failures = counts1.failures + counts2.failures,
        successes = counts1.successes + counts2.successes,
    )
}

shape TestCounts {
    failures: Int,
    successes: Int,
}

fun printFailures(result: TestResult) !Io -> Unit {
    // TODO: formatting of suites
    when (result) {
        is TestCaseResult {
            val error = result.error;
            if (error is Options.Some[AssertionError]) {
                print!("\n");
                print!(redCode);
                print!("✖ ");
                print!(result.name);
                print!(":\n");
                print!(error.value.message);
                print!(blackCode);
                print!("\n\n");
            }
        }
        is TestSuiteResult {
            Lists.forEach!(printFailures, result.results);
        }
    }
}

fun runTest(test: Test, *, parent: Options.Option[String]) !Io -> TestResult {
    val namePrefix = when (parent) {
        is Options.Some[String] {
            parent.value + ": "
        }
        is Options.None {
            ""
        }
    };

    when (test) {
        is TestCase {
            val result = test.run();
            val error = result.error;

            when (error) {
                is Options.Some[AssertionError] {
                    print!(redCode);
                    print!("✖");
                }
                is Options.None {
                    print!(greenCode);
                    print!("✔");
                }
            }
            print!(blackCode);
            TestCaseResult(name = namePrefix + test.name, error = result.error)
        }
        is TestSuite {
            // TODO: indentation
            val name = namePrefix + test.name;
            val results = Lists.map!(runTest ~ (parent = Options.some(name)), test.tests);
            TestSuiteResult(name = name, results = results)
        }
    }
}
