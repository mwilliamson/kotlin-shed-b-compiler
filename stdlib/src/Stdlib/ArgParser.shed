export ParseResult, Success, Failure, parser, parse, positional, option, optional, string;

import ListBuilder from .ListBuilder;
import ListLookup from .ListLookup;
import Lists from .Lists;
import Options from .Options;
import Sequences from .Sequences;
import UnicodeScalars from .Strings.UnicodeScalars;

shape Parser[T] {
    argDefinitions: ArgDefinitions,
    parseStringArgs: Fun (StringArgs) !ParseEffect -> T,
}

shape ArgDefinitions {
    positional: ListBuilder.Builder[ArgDefinition],
    options: ListBuilder.Builder[#(String, ArgDefinition)],
}

shape ArgDefinition {
    name: String,
    isOption: Bool,
}

shape ArgType[T] {
    parse: Fun (.name: String, .argument: Option[String]) !ParseEffect -> T,
}

union ParseResult[+T] =
    | Success[T] {
        value: T,
    }
    | Failure {
        message: String,
    };

effect ParseEffect {
    .fail: (String) -> Nothing,
}

val string = ArgType(
    .parse = fun (.name: String, .argument: Option[String]) !ParseEffect -> String {
        when (argument) {
            is Some {
                argument.value
            }
            is None {
                ParseEffect.fail!("required argument is missing: " + name)
            }
        }
    },
);

fun optional[T](argType: ArgType[T]) -> ArgType[Option[T]] {
    ArgType(
        .parse = fun(.name: String, .argument: Option[String]) !ParseEffect -> Option[T] {
            when (argument) {
                is Some {
                    val result = argType.parse!(.name = name, .argument = argument);
                    some(result)
                }
                is None {
                    none
                }
            }
        }
    )
}

type StringArgs = ListLookup.ListLookup[String, String];

fun parser[T](initialValue: T) -> Parser[T] {
    Parser(
        .argDefinitions = ArgDefinitions(
            .positional = ListBuilder.builder(),
            .options = ListBuilder.builder(),
        ),
        .parseStringArgs = fun (stringArgs: StringArgs) => initialValue,
    )
}

fun parse[T](parser: Parser[T], args: Lists.List[String]) -> ParseResult[T] {
    handle ParseEffect {
        val stringArgs = toStringArgs!(
            .positional = ListBuilder.toList(parser.argDefinitions.positional),
            .options = ListLookup.create(UnicodeScalars.eq, ListBuilder.toList(parser.argDefinitions.options)),
            .args = args,
        );
        val result = parser.parseStringArgs!(ListLookup.create(UnicodeScalars.eq, stringArgs));
        Success(.value = result)
    } on {
        .fail = (message: String) {
            exit Failure(.message = message)
        }
    }
}

fun toStringArgs(
    .positional: Lists.List[ArgDefinition],
    .options: ListLookup.ListLookup[String, ArgDefinition],
    .args: Lists.List[String],
) !ParseEffect -> Lists.List[#(String, String)] {
    // TODO: make this vaguely pleasant
    when (args) {
        is Lists.Cons {
            if (UnicodeScalars.startsWith("--", args.head)) {
                val optionName = args.head;
                val argDefinitionLookupResult = ListLookup.lookup(options, optionName);
                when (argDefinitionLookupResult) {
                    is Some {
                        val argDefinition = argDefinitionLookupResult.value;
                        val tail = args.tail;
                        when (tail) {
                            is Lists.Cons {
                                Lists.cons(
                                    #(argDefinition.name, tail.head),
                                    toStringArgs!(
                                        .positional = positional,
                                        .options = options,
                                        .args = tail.tail,
                                    ),
                                )
                            }
                            is Lists.Nil {
                                ParseEffect.fail!("option is missing value: " + optionName)
                            }
                        }
                    }
                    is None {
                        ParseEffect.fail!("unknown option: " + optionName)
                    }
                }
            } else {
                when (positional) {
                    is Lists.Cons {
                        val argDefinition = positional.head;
                        Lists.cons(
                            #(argDefinition.name, args.head),
                            toStringArgs!(
                                .positional = positional.tail,
                                .options = options,
                                .args = args.tail,
                            ),
                        )
                    }
                    is Lists.Nil {
                        ParseEffect.fail!("extra positional argument: " + args.head)
                    }
                }
            }
        }
        is Lists.Nil {
            Lists.list()
        }
    }
}

fun positional[A, T, R](
    name: String,
    argType: ArgType[A],
    add: Fun (A, T) -> R,
    parser: Parser[T],
) -> Parser[R] {
    val argDefinition = ArgDefinition(
        .name = name,
        .isOption = false,
    );

    Parser[R](
        .argDefinitions = ArgDefinitions.fields.positional.update(
            ListBuilder.append(parser.argDefinitions.positional, argDefinition),
            parser.argDefinitions,
        ),
        .parseStringArgs = fun (stringArgs: StringArgs) !ParseEffect {
            val otherArgs = parser.parseStringArgs!(stringArgs);
            val stringArg = ListLookup.lookup(stringArgs, name);
            val value = argType.parse!(.name = name, .argument = stringArg);
            add(value, otherArgs)
        },
    )
}

fun option[A, T, R](
    name: String,
    argType: ArgType[A],
    add: Fun (A, T) -> R,
    parser: Parser[T],
) -> Parser[R] {
    val argDefinition = ArgDefinition(
        .name = name,
        .isOption = true,
    );

    Parser[R](
        .argDefinitions = ArgDefinitions.fields.options.update(
            ListBuilder.append(parser.argDefinitions.options, #(name, argDefinition)),
            parser.argDefinitions,
        ),
        .parseStringArgs = fun (stringArgs: StringArgs) !ParseEffect {
            val otherArgs = parser.parseStringArgs!(stringArgs);
            val stringArg = ListLookup.lookup(stringArgs, name);
            val value = argType.parse!(.name = name, .argument = stringArg);
            add(value, otherArgs)
        },
    )
}
