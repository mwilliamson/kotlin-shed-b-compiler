export ParseResult, Success, Failure, parser, parse, positional, option, flag, optional, string, map;

import ListBuilder from .ListBuilder;
import ListLookup from .ListLookup;
import Lists from .Lists;
import Options from .Options;
import Sequences from .Sequences;
import UnicodeScalars from .Strings.UnicodeScalars;

shape Parser[T] {
    argDefinitions: ArgDefinitions,
    parseStringArgs: Fun (StringArgs) !ParseEffect -> T,
}

shape ArgDefinitions {
    positional: ListBuilder.Builder[ArgDefinition],
    options: ListBuilder.Builder[#(String, ArgDefinition)],
}

shape ArgDefinition {
    name: String,
    isOption: Bool,
    isFlag: Bool,
}

shape ArgType[T] {
    parse: Fun (.name: String, .argument: Option[String]) !ParseEffect -> T,
}

union ParseResult[+T] =
    | Success[T] {
        value: T,
    }
    | Failure {
        message: String,
    };

effect ParseEffect {
    .fail: (String) -> Nothing,
}

val string = ArgType(
    .parse = fun (.name: String, .argument: Option[String]) !ParseEffect -> String {
        when (argument) {
            is Some {
                argument.value
            }
            is None {
                ParseEffect.fail!("required argument is missing: " + name)
            }
        }
    },
);

fun optional[T](argType: ArgType[T]) -> ArgType[Option[T]] {
    ArgType(
        .parse = fun(.name: String, .argument: Option[String]) !ParseEffect -> Option[T] {
            when (argument) {
                is Some {
                    val result = argType.parse!(.name = name, .argument = argument);
                    some(result)
                }
                is None {
                    none
                }
            }
        }
    )
}

type StringArgs = ListLookup.ListLookup[String, String];

fun parser[T](initialValue: T) -> Parser[T] {
    Parser(
        .argDefinitions = ArgDefinitions(
            .positional = ListBuilder.builder(),
            .options = ListBuilder.builder(),
        ),
        .parseStringArgs = fun (stringArgs: StringArgs) => initialValue,
    )
}

fun parse[T](parser: Parser[T], args: Lists.List[String]) -> ParseResult[T] {
    handle ParseEffect {
        val stringArgs = toStringArgs!(
            .positionals = ListBuilder.toList(parser.argDefinitions.positional),
            .options = ListLookup.create(UnicodeScalars.eq, ListBuilder.toList(parser.argDefinitions.options)),
            .args = args,
        );
        val result = parser.parseStringArgs!(ListLookup.create(UnicodeScalars.eq, stringArgs));
        Success(.value = result)
    } on {
        .fail = (message: String) {
            exit Failure(.message = message)
        }
    }
}

fun toStringArgs(
    .positionals: Lists.List[ArgDefinition],
    .options: ListLookup.ListLookup[String, ArgDefinition],
    .args: Lists.List[String],
) !ParseEffect -> Lists.List[#(String, String)] {
    effect E {
        .fail: (String) -> Nothing,
        .done: () -> Nothing,
        .nextString: () -> Option[String],
        .lookupOption: (String) -> ArgDefinition,
        .storeOption: (ArgDefinition, String) -> Unit,
        .storePositional: (String) -> Unit,
    }

    shape State {
        args: Lists.List[String],
        positionals: Lists.List[ArgDefinition],
        result: Lists.List[#(String, String)],
    }

    val initialState = State(
        .args = args,
        .positionals = positionals,
        .result = Lists.nil,
    );

    fun readArguments() !E -> Nothing {
        forever!(fun () !E {
            readNextArgument!();
        })
    }

    fun readNextArgument() !E -> Unit {
        val arg = nextStringOrDone!();

        if (UnicodeScalars.startsWith("--", arg)) {
            readOption!(arg);
        } else {
            E.storePositional!(arg);
        }
    }

    fun nextStringOrDone() !E -> String {
        val arg = E.nextString!();
        when (arg) {
            is Some(.value as arg) {
                arg
            }
            is None {
                E.done!()
            }
        }
    }

    fun nextStringOrFail(message: String) !E -> String {
        val arg = E.nextString!();
        when (arg) {
            is Some(.value as arg) {
                arg
            }
            is None {
                E.fail!(message)
            }
        }
    }

    fun readOption(arg: String) !E -> Unit {
        // TODO: better string splitting
        val index = UnicodeScalars.stringToSequence(arg)
            |> Sequences.findIndex~(fun (scalar: UnicodeScalar) => scalar == '=');

        when (index) {
            is Some(.value as index) {
                val optionName = UnicodeScalars.left(index, arg);
                val argValue = UnicodeScalars.dropLeft(index + 1, arg);
                readOptionWithImmediateValue!(optionName, argValue);
            }
            is None {
                readOptionWithoutImmediateValue!(arg);
            }
        }
    }

    fun readOptionWithImmediateValue(optionName: String, argValue: String) !E -> Unit {
        val option = E.lookupOption!(optionName);
        if (option.isFlag) {
            E.fail!("flag does not take value: " + optionName);
        } else {
            E.storeOption!(option, argValue);
        }
    }

    fun readOptionWithoutImmediateValue(optionName: String) !E -> Unit {
        val option = E.lookupOption!(optionName);
        if (option.isFlag) {
            E.storeOption!(option, "");
        } else {
            val argValue = nextStringOrFail!("option is missing value: " + option.name);
            E.storeOption!(option, argValue);
        }
    }

    handle E withstate(initialState) {
        readArguments!()
    } on {
        .fail = (state: State, message: String) {
            ParseEffect.fail!(message)
        },

        .done = (state: State) {
            exit state.result
        },

        .nextString = (state: State) {
            val args = state.args;
            when (args) {
                is Lists.Cons {
                    val newState = State(...state, .args = args.tail);
                    resume some(args.head) withstate newState
                }
                is Lists.Nil {
                    resume none withstate state
                }
            }
        },

        .lookupOption = (state: State, name: String) {
            when (ListLookup.lookup(options, name)) {
                is Some(.value as option) {
                    resume option withstate state
                }
                is None {
                    ParseEffect.fail!("unknown option: " + name)
                }
            }
        },

        .storeOption = (state: State, option: ArgDefinition, value: String) {
            val newResult = Lists.cons(#(option.name, value), state.result);
            val newState = State(...state, .result = newResult);
            resume unit withstate newState
        },

        .storePositional = (state: State, value: String) {
            val positionals = state.positionals;
            when (positionals) {
                is Lists.Cons {
                    val newResult = Lists.cons(#(positionals.head.name, value), state.result);
                    val newState = State(
                        ...state,
                        .positionals = positionals.tail,
                        .result = newResult,
                    );
                    resume unit withstate newState
                }
                is Lists.Nil {
                    ParseEffect.fail!("extra positional argument: " + value)
                }
            }
        },
    }
}

fun positional[A, T, R](
    name: String,
    argType: ArgType[A],
    add: Fun (A, T) -> R,
    parser: Parser[T],
) -> Parser[R] {
    val argDefinition = ArgDefinition(
        .name = name,
        .isOption = false,
        .isFlag = false,
    );

    Parser[R](
        .argDefinitions = ArgDefinitions(
            ...parser.argDefinitions,
            .positional = ListBuilder.append(parser.argDefinitions.positional, argDefinition),
        ),
        .parseStringArgs = fun (stringArgs: StringArgs) !ParseEffect {
            val otherArgs = parser.parseStringArgs!(stringArgs);
            val stringArg = ListLookup.lookup(stringArgs, name);
            val value = argType.parse!(.name = name, .argument = stringArg);
            add(value, otherArgs)
        },
    )
}

fun option[A, T, R](
    name: String,
    argType: ArgType[A],
    add: Fun (A, T) -> R,
    parser: Parser[T],
) -> Parser[R] {
    val argDefinition = ArgDefinition(
        .name = name,
        .isOption = true,
        .isFlag = false,
    );

    Parser[R](
        .argDefinitions = ArgDefinitions(
            ...parser.argDefinitions,
            .options = ListBuilder.append(parser.argDefinitions.options, #(name, argDefinition)),
        ),
        .parseStringArgs = fun (stringArgs: StringArgs) !ParseEffect {
            val otherArgs = parser.parseStringArgs!(stringArgs);
            val stringArg = ListLookup.lookup(stringArgs, name);
            val value = argType.parse!(.name = name, .argument = stringArg);
            add(value, otherArgs)
        },
    )
}

fun flag[T, R](
    name: String,
    add: Fun (Bool, T) -> R,
    parser: Parser[T],
) -> Parser[R] {
    val argDefinition = ArgDefinition(
        .name = name,
        .isOption = true,
        .isFlag = true,
    );

    Parser[R](
        .argDefinitions = ArgDefinitions(
            ...parser.argDefinitions,
            .options = ListBuilder.append(parser.argDefinitions.options, #(name, argDefinition)),
        ),
        .parseStringArgs = fun (stringArgs: StringArgs) !ParseEffect {
            val otherArgs = parser.parseStringArgs!(stringArgs);
            val stringArg = ListLookup.lookup(stringArgs, name);
            val value = stringArg is Some;
            add(value, otherArgs)
        },
    )
}

fun map[T, R](f: Fun (T) -> R, parser: Parser[T]) -> Parser[R] {
    Parser[R](
        .argDefinitions = parser.argDefinitions,
        .parseStringArgs = fun (stringArgs) !_ {
            val result = parser.parseStringArgs!(stringArgs);
            f(result)
        },
    )
}

fun forever[!E](func: Fun () !E -> Any) !E -> Nothing {
    func!();
    tailrec forever!(func)
}
