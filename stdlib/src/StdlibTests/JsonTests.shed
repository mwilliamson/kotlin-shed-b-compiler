export testCases;

import Stdlib.Json;
import Stdlib.Matchers;
import Stdlib.Options;
import Stdlib.Tests;

type JsonValueMatcher = Matchers.Matcher[Json.JsonValue];

val isFailure = Matchers.Matcher(
    description = "failure",
    match = fun (actual: Json.JsonResult) -> Matchers.Result {
        when (actual) {
            is Json.JsonSuccess {
                Matchers.failure("was success")
            }
            is Json.JsonFailure {
                Matchers.success
            }
        }
    },
);

fun isSuccess(value: JsonValueMatcher) -> Matchers.Matcher[Json.JsonResult] {
    Matchers.Matcher(
        description = "success with value " + value.description,
        match = fun (actual: Json.JsonResult) -> Matchers.Result {
            when (actual) {
                is Json.JsonSuccess {
                    value.match(actual.value)
                }
                is Json.JsonFailure {
                    Matchers.failure("was failure")
                }
            }
        },
    )
}

fun hasJsonText(expectedText: String) -> JsonValueMatcher {
    Matchers.Matcher(
        description = "has JSON text: " + expectedText,
        match = fun (actual: Json.JsonValue) -> Matchers.Result {
            val text = Json.valueToText(actual);
            if (text == expectedText) {
                Matchers.success
            } else {
                Matchers.failure("text was: " + text)
            }
        },
    )
}

fun isJson(*, description: String, predicate: Fun (Json.JsonValue) -> Bool) -> Matchers.Matcher[Json.JsonValue] {
    Matchers.Matcher(
        description = description,
        match = fun (actual: Json.JsonValue) -> Matchers.Result {
            if (predicate(actual)) {
                Matchers.success
            } else {
                Matchers.failure("was " + Json.valueToText(actual))
            }
        },
    )
}

val isJsonNull = isJson(
    description = "null",
    predicate = fun (actual: Json.JsonValue) -> Bool {
        actual is Json.JsonNull
    },
);

fun isJsonBoolean(value: Bool) -> Matchers.Matcher[Json.JsonValue] {
    isJson(
        description = if (value) { "true" } else { "false" },
        predicate = fun (actual: Json.JsonValue) -> Bool {
            if (actual is Json.JsonBoolean) {
                actual.value == value
            } else {
                false
            }
        },
    )
}

fun isJsonArray(elements: Matchers.Matcher[List[Json.JsonValue]]) -> Matchers.Matcher[Json.JsonValue] {
    isJson(
        description = "JSON array with elements: " + elements.description,
        predicate = fun (actual: Json.JsonValue) -> Bool {
            if (actual is Json.JsonArray) {
                Matchers.isSuccess(elements.match(actual.elements))
            } else {
                false
            }
        }
    )
}

fun isJsonString(value: String) -> Matchers.Matcher[Json.JsonValue] {
    isJson(
        description = Json.valueToText(Json.string(value)),
        predicate = fun (actual: Json.JsonValue) -> Bool {
            if (actual is Json.JsonString) {
                actual.value == value
            } else {
                false
            }
        },
    )
}

val testCases = Tests.suite(moduleName, list(
    Tests.suite("valueToText", list(
        Tests.testCase("null", fun () {
            val value = Json.null;
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("null"))
        }),

        Tests.testCase("true", fun () {
            val value = Json.boolean(true);
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("true"))
        }),

        Tests.testCase("false", fun () {
            val value = Json.boolean(false);
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("false"))
        }),

        Tests.testCase("number", fun () {
            val value = Json.number("1.2");
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("1.2"))
        }),

        Tests.testCase("empty string", fun () {
            val value = Json.string("");
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("\"\""))
        }),

        Tests.testCase("string without special characters", fun () {
            val value = Json.string("apples");
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("\"apples\""))
        }),

        Tests.testCase("backslashes are escaped", fun () {
            val value = Json.string("\\ \\");
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("\"\\\\ \\\\\""))
        }),

        Tests.testCase("double quotes are escaped", fun () {
            val value = Json.string("\" \"");
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("\"\\\" \\\"\""))
        }),

        Tests.testCase("tabs are escaped", fun () {
            val value = Json.string("\t \t");
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("\"\\t \\t\""))
        }),

        Tests.testCase("line feeds are escaped", fun () {
            val value = Json.string("\n \n");
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("\"\\n \\n\""))
        }),

        Tests.testCase("carriage returns are escaped", fun () {
            val value = Json.string("\r \r");
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("\"\\r \\r\""))
        }),

        Tests.testCase("control codes are escaped", fun () {
            val value = Json.string("\u{B}");
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("\"\\u000B\""))
        }),

        Tests.testCase("empty array is serialised to square brackets", fun () {
            val value = Json.array(list());
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("[]"))
        }),

        Tests.testCase("singleton array is serialised to square brackets containing element", fun () {
            val value = Json.array(list(Json.null));
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("[null]"))
        }),

        Tests.testCase("array elements are comma separated", fun () {
            val value = Json.array(list(Json.null, Json.boolean(true), Json.boolean(false)));
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("[null,true,false]"))
        }),

        Tests.testCase("empty object is serialised to curly braces", fun () {
            val value = Json.object(list());
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("{}"))
        }),

        Tests.testCase("singleton object is serialised to curly braces containing property", fun () {
            val value = Json.object(list(Json.property("apples", Json.null)));
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("{\"apples\":null}"))
        }),

        Tests.testCase("object properties are comma separated", fun () {
            val value = Json.object(list(
                Json.property("apples", Json.null),
                Json.property("bananas", Json.boolean(true)),
                Json.property("coconut", Json.boolean(false)),
            ));
            val text = Json.valueToText(value);
            Tests.assertThat(text, Matchers.isString("{\"apples\":null,\"bananas\":true,\"coconut\":false}"))
        }),
    )),

    Tests.suite("textToValue", list(
        Tests.testCase("null", fun () {
            val text = "null";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(isJsonNull))
        }),

        Tests.testCase("true", fun () {
            val text = "true";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(isJsonBoolean(true)))
        }),

        Tests.testCase("false", fun () {
            val text = "false";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(isJsonBoolean(false)))
        }),

        Tests.testCase("empty string", fun () {
            val text = "\"\"";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(isJsonString("")))
        }),

        Tests.testCase("single character string", fun () {
            val text = "\"a\"";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(isJsonString("a")))
        }),

        Tests.testCase("multiple character string", fun () {
            val text = "\"aasfcasf\"";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(isJsonString("aasfcasf")))
        }),

        Tests.testCase("empty array", fun () {
            val text = "[]";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(hasJsonText("[]")))
        }),

        Tests.testCase("array with single element", fun () {
            val text = "[true]";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(hasJsonText("[true]")))
        }),

        Tests.testCase("array with two elements", fun () {
            val text = "[true, false]";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(hasJsonText("[true,false]")))
        }),

        Tests.testCase("array with three elements", fun () {
            val text = "[true, false, null]";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(hasJsonText("[true,false,null]")))
        }),

        Tests.testCase("unclosed array causes failure", fun () {
            val text = "[true, false";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isFailure)
        }),

        Tests.testCase("empty object", fun () {
            val text = "{}";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(hasJsonText("{}")))
        }),

        Tests.testCase("object with one property", fun () {
            val text = "{\"first\": true}";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(hasJsonText("{\"first\":true}")))
        }),

        Tests.testCase("object with two properties", fun () {
            val text = "{\"first\": true, \"second\": false}";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(hasJsonText("{\"first\":true,\"second\":false}")))
        }),

        Tests.testCase("object with three properties", fun () {
            val text = "{\"first\": true, \"second\": false, \"third\": null}";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(hasJsonText("{\"first\":true,\"second\":false,\"third\":null}")))
        }),

        Tests.testCase("leading whitespace is ignored", fun () {
            val text = "  \n\r\tfalse";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(isJsonBoolean(false)))
        }),

        Tests.testCase("trailing whitespace is ignored", fun () {
            val text = "false  \n\r\t";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isSuccess(isJsonBoolean(false)))
        }),

        Tests.testCase("text after top-level value causes failure", fun () {
            val text = "false true";
            val result = Json.textToValue(text);
            Tests.assertThat(result, isFailure)
        }),
    )),
));
